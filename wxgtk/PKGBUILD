# Maintainer: Andriy Byelikov <andriybyelikov@gmail.com>

pkgbase=wxgtk
pkgname=(wxgtk-common wxgtk2 wxgtk3 wx-utils)
_wx_release=3.0
pkgver=$_wx_release.5.1
pkgrel=1
arch=('x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(libpng libjpeg-turbo libtiff gtk2 libgl glu gst-plugins-base gtk3)
options=('!emptydirs')
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v$pkgver/wxWidgets-$pkgver.tar.bz2"
        "make-abicheck-non-fatal.patch")
sha256sums=('440f6e73cf5afb2cbf9af10cec8da6cdd3d3998d527598a53db87099524ac807'
            '46a1bb97d69163547da13d5e23a4c73e68de27ee601da5d2fb5bc5c417931453')

prepare() {
  cd wxWidgets-$pkgver
  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

_build() {
  mkdir -p gtk$1-build gtk$1-install
  cd gtk$1-build
  ../configure --prefix=/usr --with-opengl --enable-graphics_ctx \
    --enable-mediactrl --with-regex=builtin --disable-precomp-headers \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-gtk=$@
  make DESTDIR="$srcdir"/wxWidgets-$pkgver/gtk$1-install install
}

build() {
  cd wxWidgets-$pkgver
  make -C locale allmo
  _build 2
  cd ..
  _build 3 --enable-webview
}

_package() {
  cd wxWidgets-$pkgver/gtk$1-install
  shift
  cp -r --preserve=mode --parents $@ "$pkgdir"
  cd ../docs
  install -Dm644 licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk-common() {
  pkgdesc="wxWidgets base libraries, headers and gettext catalogs"
  depends=(expat gcc-libs zlib)

  _package 2 ./usr/{include/,lib/libwx_base*,share/locale}
}

package_wxgtk2() {
  pkgdesc="wxGTK 2 libraries, config script and setup.h"
  depends=(cairo gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk2
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxxf86vm
    pango wxgtk-common)

  _package 2 ./usr/lib/{libwx_gtk*,wx}
}

package_wxgtk3() {
  pkgdesc="wxGTK 3 libraries, config script and setup.h"
  depends=(cairo gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk3
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxxf86vm
    pango wxgtk-common)
  optdepends=('webkit2gtk: for webview support')

  _package 3 ./usr/lib/{libwx_gtk*,wx}
}

package_wx-utils() {
  pkgdesc="wxWidgets utilities"
  depends=(wxgtk-common)
  install=wxgtk.install

  _package 2 ./usr/{bin/wxrc-$_wx_release,share/{aclocal,bakefile}}
  for file in "$pkgdir"/usr/share/{aclocal,bakefile/presets}/*
    do mv $file "$(echo $file | sed 's/\./-'$_wx_release'./')"; done
}
