# Maintainer: Andriy Byelikov <andriybyelikov@gmail.com>

pkgbase=wxgtk-git
pkgname=(wxgtk-common-git wxgtk2-git wxgtk3-git wx-utils-git)
_wx_release=3.1
pkgver=$_wx_release.5
pkgrel=1
arch=('x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(git libpng libjpeg-turbo libtiff gtk2 libgl glu gst-plugins-base gtk3)
options=('!emptydirs')
source=("git+https://github.com/wxWidgets/wxWidgets.git"
        "make-abicheck-non-fatal.patch")
sha256sums=('SKIP'
            '46a1bb97d69163547da13d5e23a4c73e68de27ee601da5d2fb5bc5c417931453')

prepare() {
  cd wxWidgets
  git submodule update --init 3rdparty/catch
  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

_build() {
  mkdir -p gtk$1-build gtk$1-install
  cd gtk$1-build
  ../configure --prefix=/usr --with-opengl --enable-graphics_ctx \
    --enable-mediactrl --with-regex=builtin --disable-precomp-headers \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-gtk=$@
  make DESTDIR="$srcdir"/wxWidgets/gtk$1-install install
}

build() {
  cd wxWidgets
  make -C locale allmo
  _build 2
  cd ..
  _build 3 --enable-webview
}

_package() {
  cd wxWidgets/gtk$1-install
  shift
  cp -r --preserve=mode --parents $@ "$pkgdir"
  cd ../docs
  install -Dm644 licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk-common-git() {
  pkgdesc="wxWidgets base libraries, headers and gettext catalogs"
  #depends=(curl expat gcc-libs glib2 glibc libsecret xz zlib)

  _package 2 ./usr/{include/,lib/libwx_base*,share/locale/}
}

package_wxgtk2-git() {
  pkgdesc="wxGTK 2 libraries, config script and setup.h"
  #depends=(wxgtk-common-git cairo fontconfig gdk-pixbuf2 gst-plugins-base-libs
  #  gstreamer gtk2 libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11
  #  libxtst libxxf86vm pango)

  _package 2 ./usr/lib/{libwx_gtk*,wx/}
}

package_wxgtk3-git() {
  pkgdesc="wxGTK 3 libraries, config script and setup.h"
  #depends=(wxgtk-common-git cairo fontconfig gdk-pixbuf2 gst-plugins-base-libs
  #  gstreamer gtk3 libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11
  #  libxtst libxxf86vm pango wayland webkit2gtk)

  _package 3 ./usr/lib/{libwx_gtk*,wx/}
}

package_wx-utils-git() {
  pkgdesc="wxWidgets utils"
  #depends=(wxgtk-common-git)

  _package 2 ./usr/{bin/wxrc-$_wx_release,share/{aclocal/,bakefile/}}
  mv "$pkgdir"/usr/bin/wxrc-$_wx_release "$pkgdir"/usr/bin/wxrc
  ln -sf /usr/lib/wx/config/gtk3-unicode-$_wx_release "$pkgdir"/usr/bin/wx-config
}
