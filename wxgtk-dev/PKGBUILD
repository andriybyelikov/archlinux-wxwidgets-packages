# Maintainer: Andriy Byelikov <andriybyelikov@gmail.com>

pkgbase=wxgtk-dev
pkgname=(wx-runtime-dev wxgtk2-runtime-dev wxgtk3-runtime-dev
         wx-devel-dev wxgtk2-devel-dev wxgtk3-devel-dev)
_wx_release=3.1
pkgver=$_wx_release.5
pkgrel=1
arch=('x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(libpng libjpeg-turbo libtiff gtk2 libgl glu gst-plugins-base gtk3 curl libsecret)
options=('!emptydirs')
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v$pkgver/wxWidgets-$pkgver.tar.bz2"
        "make-abicheck-non-fatal.patch")
sha256sums=('d7b3666de33aa5c10ea41bb9405c40326e1aeb74ee725bb88f90f1d50270a224'
            '46a1bb97d69163547da13d5e23a4c73e68de27ee601da5d2fb5bc5c417931453')

prepare() {
  cd wxWidgets-$pkgver
  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

_build() {
  mkdir -p gtk$1-build gtk$1-install
  cd gtk$1-build
  ../configure --prefix=/usr --with-opengl --enable-graphics_ctx \
    --enable-mediactrl --with-regex=builtin --disable-precomp-headers \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-gtk=$@
  make DESTDIR="$srcdir"/wxWidgets-$pkgver/gtk$1-install install
}

build() {
  cd wxWidgets-$pkgver
  make -C locale allmo
  _build 2
  cd ..
  _build 3 --enable-webview
}

_package() {
  cd "$srcdir"/wxWidgets-$pkgver/gtk$1-install
  shift
  cp -r --preserve=mode --parents $@ "$pkgdir"
  cd ../docs
  install -Dm644 licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wx-runtime-dev() {
  pkgdesc="wxWidgets base libraries and gettext catalogs (development branch)"
  depends=(curl expat glib2 libsecret zlib)

  _package 2 ./usr/{lib/libwx_base*,share/locale}
}

package_wxgtk2-runtime-dev() {
  pkgdesc="wxGTK+2 libraries (development branch)"
  depends=(wx-runtime-dev cairo fontconfig gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk2
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxtst libxxf86vm pango)

  _package 2 ./usr/lib/libwx_gtk*
}

package_wxgtk3-runtime-dev() {
  pkgdesc="wxGTK+3 libraries (development branch)"
  depends=(wx-runtime-dev cairo fontconfig gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk3
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxtst libxxf86vm pango wayland)
  optdepends=('webkit2gtk: for webview support')

  _package 3 ./usr/lib/libwx_gtk*
}

package_wx-devel-dev() {
  pkgdesc="wxWidgets development files (development branch)"
  depends=(archlinux-wxwidgets wx-runtime-dev)
  install=install_archlinux-wxwidgets.sh

  _package 2 ./usr/include
  _package 2 ./usr/{bin/wxrc-$_wx_release,share/{aclocal,bakefile}}
  for file in "$pkgdir"/usr/share/{aclocal,bakefile/presets}/*
    do mv $file "$(echo $file | sed 's/\./-'$_wx_release'./')"; done
}

package_wxgtk2-devel-dev() {
  pkgdesc="wxGTK+2 config script and setup.h (development branch)"
  depends=(wx-devel-dev wxgtk2-runtime-dev)

  _package 2 ./usr/lib/wx/{config,include}
}

package_wxgtk3-devel-dev() {
  pkgdesc="wxGTK+3 config script and setup.h (development branch)"
  depends=(wx-devel-dev wxgtk3-runtime-dev)

  _package 3 ./usr/lib/wx/{config,include}
}
