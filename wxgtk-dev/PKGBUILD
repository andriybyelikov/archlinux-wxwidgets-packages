# Maintainer: Andriy Byelikov <andriybyelikov@gmail.com>

pkgbase=wxgtk-dev
pkgname=(wxgtk-common-dev wxgtk2-dev wxgtk3-dev wx-utils-dev)
_wx_release=3.1
pkgver=$_wx_release.5
pkgrel=1
arch=('x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(git libpng libjpeg-turbo libtiff gtk2 libgl glu gst-plugins-base gtk3)
options=('!emptydirs')
source=("git+https://github.com/wxWidgets/wxWidgets.git"
        "make-abicheck-non-fatal.patch")
sha256sums=('SKIP'
            '46a1bb97d69163547da13d5e23a4c73e68de27ee601da5d2fb5bc5c417931453')

prepare() {
  cd wxWidgets
  git submodule update --init 3rdparty/catch
  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

_build() {
  mkdir -p gtk$1-build gtk$1-install
  cd gtk$1-build
  ../configure --prefix=/usr --with-opengl --enable-graphics_ctx \
    --enable-mediactrl --with-regex=builtin --disable-precomp-headers \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-gtk=$@
  make DESTDIR="$srcdir"/wxWidgets/gtk$1-install install
}

build() {
  cd wxWidgets
  make -C locale allmo
  _build 2
  cd ..
  _build 3 --enable-webview
}

_package() {
  cd wxWidgets/gtk$1-install
  shift
  cp -r --preserve=mode --parents $@ "$pkgdir"
  cd ../docs
  install -Dm644 licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk-common-dev() {
  pkgdesc="wxWidgets base libraries, headers and gettext catalogs"
  depends=(expat gcc-libs zlib)

  _package 2 ./usr/{include/,lib/libwx_base*,share/locale}
}

package_wxgtk2-dev() {
  pkgdesc="wxGTK 2 libraries, config script and setup.h"
  depends=(cairo gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk2
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxxf86vm
    pango wxgtk-common-dev)

  _package 2 ./usr/lib/{libwx_gtk*,wx}
}

package_wxgtk3-dev() {
  pkgdesc="wxGTK 3 libraries, config script and setup.h"
  depends=(cairo gdk-pixbuf2 glib2 gst-plugins-base-libs gstreamer gtk3
    libglvnd libjpeg-turbo libnotify libpng libsm libtiff libx11 libxxf86vm
    pango wxgtk-common-dev)
  optdepends=('webkit2gtk: for webview support')

  _package 3 ./usr/lib/{libwx_gtk*,wx}
}

package_wx-utils-dev() {
  pkgdesc="wxWidgets utilities"
  depends=(wxgtk-common-dev)
  install=wxgtk.install

  _package 2 ./usr/{bin/wxrc-$_wx_release,share/{aclocal,bakefile}}
  for file in "$pkgdir"/usr/share/{aclocal,bakefile/presets}/*
    do mv $file "$(echo $file | sed 's/\./-'$_wx_release'./')"; done
}
